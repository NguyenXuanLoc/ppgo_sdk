plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    alias(libs.plugins.validator)
}

android {
    compileSdkVersion 33

    namespace 'com.pushpushgo.sdk'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaLanguageVersion.of(11)
        targetCompatibility JavaLanguageVersion.of(11)
    }

    kotlinOptions {
        jvmTarget = JavaLanguageVersion.of(11)
    }
}
kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

dependencies {
    compileOnly libs.hms.push

    compileOnly platform(libs.firebase.bom)
    compileOnly 'com.google.firebase:firebase-messaging'

    api "com.google.guava:guava:31.1-android"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configArtifactId = 'sdk-no-op'
def configGroupId = 'com.pushpushgo'

afterEvaluate {
    publishing {
        publications {
            noop(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId
                version = libs.versions.sdk.get()
            }
        }
    }
}
