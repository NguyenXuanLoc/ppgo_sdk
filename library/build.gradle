plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka' version "1.7.10"
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.11.0'
}

apply from: 'jacoco.gradle'

tasks.named("dokkaHtml") {
    moduleName.set("PushPushGo SDK")
}

android {
    compileSdkVersion 32

    namespace 'com.pushpushgo.sdk'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32

        consumerProguardFiles("ppgo-sdk.pro")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // Kodein
    implementation "org.kodein.di:kodein-di:7.15.0"

    // Kotlin Android Coroutines
    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:$coroutines_version"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.2")
    implementation 'com.squareup.okhttp3:logging-interceptor'

    def moshi_version = "1.14.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // work manager
    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"

    // firebase
    compileOnly platform('com.google.firebase:firebase-bom:31.0.1')
    compileOnly 'com.google.firebase:firebase-analytics-ktx'
    compileOnly 'com.google.firebase:firebase-messaging-ktx'

    // HMS
    compileOnly 'com.huawei.hms:push:6.7.0.300'

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.2"
    testImplementation 'org.json:json:20220320'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation 'org.robolectric:robolectric:4.9'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configArtifactId = 'sdk'
def configGroupId = 'com.pushpushgo'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId
                version = "1.3.0-SNAPSHOT"
            }
        }
    }
}
