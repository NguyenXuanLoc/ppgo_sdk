plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka' version "1.7.10"
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.11.0'
}

apply from: 'jacoco.gradle'

tasks.named("dokkaHtml") {
    moduleName.set("PushPushGo SDK")
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 12
        versionName "1.3.0-SNAPSHOT"

        consumerProguardFiles("ppgo-sdk.pro")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Kodein
    implementation "org.kodein.di:kodein-di:7.11.0"

    // Kotlin Android Coroutines
    def coroutines_version = "1.6.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:$coroutines_version"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.2")
    implementation 'com.squareup.okhttp3:logging-interceptor'

    def moshi_version = "1.13.0"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // work manager
    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"

    // firebase
    compileOnly platform('com.google.firebase:firebase-bom:30.3.0')
    compileOnly 'com.google.firebase:firebase-core'
    compileOnly 'com.google.firebase:firebase-messaging'

    // HMS
    compileOnly 'com.huawei.hms:push:6.5.0.300'

    // testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.12.4"
    testImplementation 'org.json:json:20210307'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def configArtifactId = 'sdk'
def configGroupId = 'com.pushpushgo'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = configGroupId
                artifactId = configArtifactId
                version = android.defaultConfig.versionName
            }
        }
    }
}
